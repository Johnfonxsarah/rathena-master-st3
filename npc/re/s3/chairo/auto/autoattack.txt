//
//
//	Author		Shakto
//	Version		2022/09/11
//	Web		https://ronovelty.com/
//
//	For item use, also add this to disable autoattack on unequip item
//    UnEquipScript: |
//      sc_end SC_AUTOATTACK;
//
-	script	bot_at	-1,{
	function	__onoff	;
OnInit:

	bindatcmd "botoff",strnpcinfo(3)+"::OnDo2";
	bindatcmd "botend",strnpcinfo(3)+"::OnDo2";
	end;

OnDo:
	sc_end SC_AUTOATTACK;
	.@AA_NA = 1;
	.@duration = 86400000; // 1000 = 1s -- 60000 = 1m -- 3600000 = 1h etc...

	goto(main_menu);
	end;

main_menu:
	.@menu$ = "[Auto Attack(AI)]";
	mes .@menu$;
	if (getequipid(EQI_COSTUME_HEAD_MID) == 20183){
		.@main_menu = select("[เริ่มโจมตี]","[ตั้งค่า AI]","[ยกเลิก AI]");
		if( .@main_menu == 1 ){
			sc_start4 SC_AUTOATTACK,.@duration,.@AA_NA,0,0,0;
		}
		if( .@main_menu == 2 )
			goto(config_menu);
	} else {
		mes "ขออภัยคุณไม่ได้สวมใส่ Costume Ai Auto Attack";
	}
	
	close;

config_menu:
	clear;
	.@menu$ = "[Auto Attack Configuration]";
	mes .@menu$;

	// To disable an option, comment the line
	menu	"กลับเมนูหลัก",main_menu,
			" ~ โจมตีแบบปกติ",melee_attack,
			" ~ ตั้งค่าการปั้มยา",regeneration_menu,
			" ~ ตั้งค่าให้ AI ใช้สกิลโจมตี",attackskills_menu,
			" ~ ตั้งค่าสกิลบัพ Buff",buffskills_menu,
			" ~ ตั้งค่า Item Buff",buffitems_menu,
			" ~ ตั้งค่าการ Teleport",teleport_menu,
			" ~ ตั้งค่าโจมตี Monsters ที่กำหนด",monsters_menu;
	end;

pickupitems_menu:
	clear;
	.@menu$ = "[ตั้งค่าเก็บ Item ที่กำหนด]";

	mes .@menu$;
	mes "--- การกำหนดค่าปัจจุบัน ---";
	mes getautoattackstring(9,0);

	.@pickupitems_menu = select("ตั้งค่าการเก็บ Item","เพิ่ม Item ที่ต้องการ","ลบ Item ที่ต้องการเก็บออก","ลบรายชื่อ Item ที่ต้องการเก็บออกที่งหมด","[ย้อนกลับ]");

	switch(.@pickupitems_menu){
		case 1:
			.@pickup_config_choice = 0;
			// every item = 0
			// on the list = 1
			// no item = 2
			switch(getautoattackint(9,2)){
				case 0:
					.@pickupitems_config_menu  = select("เก็บ Item เฉพาะใน List เท่านั้น","ไม่เก็บ Item เลย","[ย้อนกลับ]");
					switch(.@pickupitems_config_menu){
						case 1:
							.@pickup_config_choice = 1;
							break;
						case 2 :
							.@pickup_config_choice = 2;
							break;
						case 3 :
							goto(pickupitems_menu);
							break;
					}
						
					break;
				case 1:
					.@pickupitems_config_menu  = select("เก็บ Items ทั้งหมด","ไม่เก็บ Item เลย","[ย้อนกลับ]");
					switch(.@pickupitems_config_menu){
						case 1:
							.@pickup_config_choice = 0;
							break;
						case 2 :
							.@pickup_config_choice = 2;
							break;
						case 3 :
							goto(pickupitems_menu);
							break;
					}
					break;
				case 2:
					.@pickupitems_config_menu  = select("เก็บ Items ทั้งหมด","เก็บ Item เฉพาะใน List เท่านั้น","[ย้อนกลับ]");
					switch(.@pickupitems_config_menu){
						case 1:
							.@pickup_config_choice = 0;
							break;
						case 2 :
							.@pickup_config_choice = 1;
							break;
						case 3 :
							goto(pickupitems_menu);
							break;
					}
					break;				
			}
			setautoattack("9;2;" + .@pickup_config_choice);
			break;
		case 2:
			clear;
			mes .@menu$;
			mes "ป้อนรหัสของ Item ที่จะเก็บโดยเฉพาะเพิ่มในรายการ (สูงสุด 20 ชิ้น)";
			do {
				input .@itempickup_id;
			} while(.@itempickup_id == 0);

			setautoattack("9;1;" + .@itempickup_id);

			break;
		case 3:
			if(getautoattackint(9,1) > 0){
				mes "ใส่รหัส Item ที่คุณต้องการจะลบออก";

				do {
					input .@itempickup_id;
				} while(.@itempickup_id == 0);
				setautoattack("9;0;" + .@itempickup_id);
			} else {
				clear;
				mes .@menu$;
				mes "ขออภัย, คุณไม่ได้ตั้งค่ารายการเก็บ Item เฉพาะ";
				next;
			}

			break;
		case 4:
			setautoattack("9;-1;0");
			break;

		case 5:
			goto(config_menu);
			break;
	}

	goto(pickupitems_menu);
	end;

monsters_menu:
	clear;
	.@menu$ = "[Monsters Selection]";

	mes .@menu$;
	mes "--- สถานะตั้งค่าปัจจุบัน ---";
	mes getautoattackstring(8,0);

	.@monsters_menu = select("Add a monster","Remove a monster","Clear the list",getautoattackint(8,0)?"Attack aggressive monsters":"Ignore aggressive monsters","[Cancel]");

	switch(.@monsters_menu){
		case 1:
			clear;
			mes .@menu$;
			mes "Enter the id of the monster to add on the list (20 max)";
			do {
				input .@mob_id;
			} while(.@mob_id == 0);

			setautoattack("8;1;" + .@mob_id + ";" + getautoattackint(8,0));

			break;
		case 2:
			if(getautoattackint(8,1) > 0){
				mes "Enter the id of the monster to delete from the list";

				do {
					input .@mob_id;
				} while(.@mob_id == 0);
				setautoattack("8;0;" + .@mob_id + ";" + getautoattackint(8,0));
			} else {
				clear;
				mes .@menu$;
				mes "Sorry, you don't have any monster of this list";
				next;
			}

			break;
		case 3:
			setautoattack("8;-1;0;0");
			break;

		case 4:
			if(getautoattackint(8,0))
				setautoattack("8;0;0;0");
			else
				setautoattack("8;0;0;1");

			break;

		case 5:
			goto(config_menu);
			break;
	}

	goto(monsters_menu);
	end;

teleport_menu:
	clear;
	.@menu$ = "[Teleport]";

	mes .@menu$;
	mes "--- สถานะตั้งค่าปัจจุบัน Teleport ---";
	mes getautoattackstring(7,0);

	if(getautoattackint(7,0) == 1)
		.@teleport_menu_txt$ = "เปิดการใช้ Skill Teleport";
	else
		.@teleport_menu_txt$ = "ปิดการใช้ Skill Teleport";

	if(getautoattackint(7,1) == 1)
		.@teleport_menu_txt$ += ":เปิดการใช้ Fly Wing";
	else
		.@teleport_menu_txt$ += ":ปิดการใช้ Fly Wing";
		
	.@teleport_menu_txt$ += ":เพิ่ม / แก้ไข เมื่อ Hp เหลือเท่าไหร่แล้ววิงค์ : เพิ่ม / แก้ไข ให้วิงค์เมื่อไม่เจอ Monster:[ย้อนกลับเมนูหลัก]";

	.@teleport_menu = select(.@teleport_menu_txt$);

	switch(.@teleport_menu){
		case 1:
			if(getautoattackint(7,0))
				setautoattack("7;0;0");
			else
				setautoattack("7;0;1");
			break;
		case 2:
			if(getautoattackint(7,1))
				setautoattack("7;1;0");
			else
				setautoattack("7;1;1");
			break;
		case 3:
			do {
				clear;
				mes .@menu$;
				mes "เลือกค่าขั้นต่ำของ HP % ที่คุณจะหลบหนีฉุกเฉินเมื่อถูกโจมตี)";
				mes "เลขน้อยสุด 5% , เลขมากสุด 90% (ใส่เลข 0 ถ้าต้องการปิดการใช้)";
				input .@teleport_minhpvalue;
			} while(.@teleport_minhpvalue != 0 && (.@teleport_minhpvalue < 5 || .@teleport_minhpvalue > 90));
			setautoattack("7;2;"+.@teleport_minhpvalue);
			break;
		case 4:
			do {
				clear;
				mes .@menu$;
				mes "ใส่เลขเวลาในการ Teleport เมื่อไม่เจอ Monster)";
				mes "น้อยสุด 2 วินาที, มากสุด 600 นาที (ใส่เลข 0 ถ้าต้องการปิดการใช้)";
				input .@teleport_delayvalue;
			} while(.@teleport_delayvalue != 0 && (.@teleport_delayvalue < 2 || .@teleport_delayvalue > 600));
			setautoattack("7;3;"+.@teleport_delayvalue);
			break;
		default:
			goto(config_menu);
			break;
	}

	goto(teleport_menu);
	end;

melee_attack:
	clear;
	.@menu$ = "[Melee attack]";

	mes .@menu$;
	mes "--- สถานะตั้งค่าปัจจุบัน ---";
	mes getautoattackstring(6,0);

	.@melee_menu = select(getautoattackint(6,0)?"Enable":"Disable","[ย้อนกลับเมนูหลัก]");

	if( .@melee_menu == 1 ){
		if(getautoattackint(6,0))
			setautoattack("6;0");
		else
			setautoattack("6;1");
		clear;

		goto(melee_attack);
	}

	goto(config_menu);
	end;

regeneration_menu:
	mes "Clear menu";
	clear;
	.@menu$ = "[Regeneration]";
	
	mes .@menu$;
	menu	"ย้อนกลับเมนูหลัก",config_menu,
			" ~ Auto-Heal",heal_menu,
			" ~ ตั้งค่า การใช้ Potions HP/SP",potion_menu;

	end;

heal_menu:
	clear;
	.@menu$ = "[Auto-Heal]";
	if(!getskilllv(28) && !getskilllv(2051) && !getskilllv(231)){
		mes .@menu$;
		mes "ขออภัย คุณไม่มีทักษะ Heal";
		next;
		goto(regeneration_menu);
	}

	mes .@menu$;
	mes "--- สถานะตั้งค่าปัจจุบัน ---";
	mes getautoattackstring(0,0);

	.@heal_menu = select("เพิ่ม / แก้ไข้สกิล Heal","ปิดการใช้สกิล","[ย้อนกลับเมนูหลัก]");
	if( .@heal_menu == 1 ){
		clear;
		mes .@menu$;
		mes "กรุณาใส่เลขสกิล (ใส่แค่เลขไม่ต้องใส่ชื่อสกิล)";
		if(getskilllv(28)) // Heal
			mes "^FF0000(28)^000000 [" + getskillinfostring(0,28) + "]" + " - Max lv : " + getskilllv(28);
		if(getskilllv(2051)) // Highness Heal
			mes "^FF0000(2051)^000000 [" + getskillinfostring(0,2051) + "]" + " - Max lv : " + getskilllv(2051);
		if(getskilllv(231)) // Potion Pitcher
			mes "^FF0000(231)^000000 [" + getskillinfostring(0,231) + "]" + " - Max lv : " + getskilllv(231);

		input .@heal_skillid;

		if((.@heal_skillid != 28 && .@heal_skillid != 2051 && .@heal_skillid != 231) || !getskilllv(.@heal_skillid)){
			clear;
			mes .@menu$;
			mes "กรุณาใส่เลขเลเวลของสกิลที่ต้องการใช้";
			goto(heal_menu);
		} else {
			do{
				clear;
				mes .@menu$;
				mes "กรุณาใส่เลเวลของสกิลที่จะใช้";
				mes "[" + getskillinfostring(0,.@heal_skillid) + "]" + " - Max lv : " + getskilllv(.@heal_skillid);
				input .@heal_skilllv;
			} while(.@heal_skilllv < 1 || .@heal_skilllv > getskilllv(.@heal_skillid));

			do {
				clear;
				mes .@menu$;
				mes "ตั้งค่าให้ใช้สกิลเมื่อเหลือ HP เหลือกี่เปอร์เซ็นต์  (ตัวอย่าง : ใส่เลข 10 จะใช้สกิล Heal เมื่อ HP เหลือ 10%)";
				mes "เลขที่ใส่น้อยสุด 5%, เลขมากสุด 90%";
				mes "[" + getskillinfostring(0,.@heal_skillid) + "]" + " - Lv : " + .@heal_skilllv;
				input .@heal_minhpvalue;
			} while(.@heal_minhpvalue < 5 || .@heal_minhpvalue > 90);
			setautoattack("0;1;"+ .@heal_skillid + ";" + .@heal_skilllv + ";" + .@heal_minhpvalue);
			clear;
		}
		goto(heal_menu);
	}
	if( .@heal_menu == 2 ){
		if(getautoattackint(0,0) == 0){
			clear;
			mes .@menu$;
			mes "ไม่มีทักษะการรักษาอัตโนมัติที่ใช้งานอยู่ให้ลบ";
			next;
			goto(heal_menu);
		} else {
			mes "กรุณาใส่เลขของสกิลที่ต้องการปิดการใช้.";
			input .@heal_skillid;

			if((.@heal_skillid != 28 && .@heal_skillid != 2051 && .@heal_skillid != 231) || !getskilllv(.@heal_skillid)){
				clear;
				mes .@menu$;
				mes "โปรดเลือกทักษะที่มีอยู่";
				goto(heal_menu);
			} else 
				setautoattack("0;0;"+ .@heal_skillid + ";0;0");
			goto(heal_menu);
		}
	}

	goto(regeneration_menu);
	end;

potion_menu:
	clear;
	.@menu$ = "[HP / SP Potions]";
	if(getautoattackint(1,0) == 0){
		mes .@menu$;
		mes "ขออภัย คุณไม่มีไอเทมรักษาใดๆ";
		next;
		goto(regeneration_menu);
	}

	mes .@menu$;
	mes "--- สถานะตั้งค่าปัจจุบัน ---";
	mes getautoattackstring(1,0);

	.@potion_menu = select("เพิ่ม / แก้ไข Item Potions","ปิดการใช้ Item Potion","[ย้อนกลับเมนูหลัก]");
	if( .@potion_menu == 1 ){
		clear;
		mes .@menu$;
		mes getautoattackstring(1,-1);
		mes "กรุณาใส่เลข Item Potion ที่ต้องการใช้";

		input .@potion_id;

		do {
			clear;
			mes .@menu$;
			mes "ตั้งค่าการใช้ Item Potion เมื่อ HP เหลือกี่ เปอร์เซ็นต์  (ตัวอย่าง : ใส่เลข 10 AI จะใช้ Potion เมื่อ HP เหลือ 10%)";
			mes "เลขน้อยสุด 5, เลขมากสุด 90 (หากเป็น Item เพิ่ม SP ให้ใส่เลข 0)";
			input .@potion_minhpvalue;
		} while(.@potion_minhpvalue != 0 && (.@potion_minhpvalue < 5 || .@potion_minhpvalue > 90));

		do {
			clear;
			mes .@menu$;
			mes "ตั้งค่าการใช้ Item Potion เมื่อ SP เหลือกี่ เปอร์เซ็นต์  (ตัวอย่าง : ใส่เลข 10 AI จะใช้ Potion เมื่อ SP เหลือ 10%)";
			mes "เลขน้อยสุด 5, เลขมากสุด 90 (หากเป็น Item เพิ่ม HP ให้ใส่เลข 0)";
			input .@potion_minspvalue;
		} while(.@potion_minspvalue != 0 && (.@potion_minspvalue < 5 || .@potion_minspvalue > 90));

		setautoattack("1;1;"+ .@potion_id + ";" + .@potion_minhpvalue + ";" + .@potion_minspvalue);
		clear;

		goto(potion_menu);
	}
	if( .@potion_menu == 2 ){
		if(getautoattackint(1,0) == 0){
			clear;
			mes .@menu$;
			mes "ไม่มีรายการยาที่ใช้งานอยู่ให้ลบ";
			next;
			goto(potion_menu);
		} else {
			mes "กรุณากรอกรหัส Item ที่ตั้งค่าไว้เพื่อปิดการใช้งาน.";
			input .@potion_id;
			setautoattack("1;0;"+ .@potion_id + ";0;0");
			goto(potion_menu);
		}
	}

	goto(regeneration_menu);
	end;

sit_regen_menu:
	clear;
	.@menu$ = "[ตั้งค่าการนั่ง]";

	mes .@menu$;
	mes "--- สถานะตั้งค่าปัจจุบัน ---";
	mes getautoattackstring(2,0); // status
	mes getautoattackstring(2,1); // hp
	mes getautoattackstring(2,2); // sp

	.@sit_regen_menu = select(getautoattackint(2,0)?"Disable:Modify":"Enable","[Cancel]");
	if( (.@sit_regen_menu == 1 && !getautoattackint(2,0))
		|| (.@sit_regen_menu == 2 && getautoattackint(2,0))){
		clear;
		mes .@menu$;
		mes "Choose the value of HP % to sit (ex : 10 to sit if hp are less than 10%)";
		mes "Min value is 5, Max value is 90 (0 to disable it)";

		do {
			input .@sit_regen_minhpvalue;
		} while(.@sit_regen_minhpvalue != 0 && (.@sit_regen_minhpvalue < 5 || .@sit_regen_minhpvalue > 90));

		if(.@sit_regen_minhpvalue > 0){
			clear;
			mes .@menu$;
			mes "ตั้งค่าการนั่งเมื่อ HP เหลือกี่เปอร์เซ็นต์  (ตัวอย่าง : ใส่เลข 10 AI จะใช้ Potion เมื่อ SP เหลือ 10%)";
			mes "Min value is 5, Max value is 90 (0 to disable it)";
			mes "Must be more than min hp value : " + .@sit_regen_minhpvalue;

			do {
				input .@sit_regen_maxhpvalue;
			} while((.@sit_regen_maxhpvalue != 0 && (.@sit_regen_maxhpvalue < 5 || .@sit_regen_maxhpvalue > 90)) || (.@sit_regen_minhpvalue >= .@sit_regen_maxhpvalue));
		}

		clear;
		mes .@menu$;
		mes "Choose the value of SP % to sit (ex : 10 to sit if sp are less than 10%)";
		mes "Min value is 5, Max value is 90 (0 to disable it)";

		do {
			input .@sit_regen_minspvalue;
		} while(.@sit_regen_minspvalue != 0 && (.@sit_regen_minspvalue < 5 || .@sit_regen_minspvalue > 90));

		if(.@sit_regen_minspvalue > 0){
			clear;
			mes .@menu$;
			mes "Choose the value of SP % to stand (ex : 10 to stand if sp are more than 10%)";
			mes "Min value is 5, Max value is 90 (0 to disable it)";
			mes "Must be more than min sp value : " + .@sit_regen_minspvalue;

			do {
				input .@sit_regen_maxspvalue;
			} while((.@sit_regen_maxspvalue != 0 && (.@sit_regen_maxspvalue < 5 || .@sit_regen_maxspvalue > 90)) || (.@sit_regen_minspvalue >= .@sit_regen_maxspvalue));
		}
		
		setautoattack("2;1;"+ .@sit_regen_minhpvalue + ";" + .@sit_regen_maxhpvalue + ";" + .@sit_regen_minspvalue + ";" + .@sit_regen_maxspvalue);

	} else if( .@sit_regen_menu == 1 && getautoattackint(2,0)){
		setautoattack("2;0;0;0;0;0");
	} else {
		clear;
		goto(regeneration_menu);
	}

	clear;
	goto(sit_regen_menu);
	end;

buffskills_menu:
	clear;
	.@menu$ = "[Buff Skills]";

	mes .@menu$;
	mes "--- สถานะตั้งค่าสกิล Buff ปัจจุบัน ---";
	mes getautoattackstring(3,0);
	
	.@buffskills_menu = select("เพิ่ม Skill Buff","แก้ไข เลเวล Skill Buff","ปิดการใช้ Skill Buff","[ย้อนกลับเมนูหลัก]");

	if( .@buffskills_menu < 3 ){
		if( .@buffskills_menu == 1 ){
			clear;
			mes .@menu$;
			mes "กรอกเลข ID ของทักษะที่เลือก";
			mes "--- รายการทักษะที่มีอยู่ ---";
			mes getautoattackstring(3,-1);
		} else {
			if(getautoattackint(3,0) == 0){
				mes .@menu$;
				mes "ไม่มีทักษะบัฟอัตโนมัติให้แก้ไข";
				next;
				goto(buffskills_menu);
			}
			mes "กรอกเลข ID ของทักษะที่เลือก";
		}

		input .@buff_skillid;
		if(!getskillinfoint(1,.@buff_skillid) || !getskilllv(.@buff_skillid)){
			clear;
			mes .@menu$;
			mes "ขออภัย คุณไม่มีทักษะนั้นหรือทักษะนี้ไม่มีอยู่";
			next;
			goto(buffskills_menu);
		}	

		do{
			clear;
			mes .@menu$;
			mes "เลือกเลเวลทักษะที่จะใช้";
			mes "[" + getskillinfostring(0,.@buff_skillid) + "]" + " - Max lv : " + getskilllv(.@buff_skillid);
			input .@buff_skilllv;
		} while(.@buff_skilllv < 1 || .@buff_skilllv > getskilllv(.@buff_skillid));

		setautoattack("3;1;"+ .@buff_skillid + ";" + .@buff_skilllv);
		clear;

		goto(buffskills_menu);
	} else if( .@buffskills_menu == 3 ){
		if(getautoattackint(3,0) == 0){
			mes .@menu$;
			mes "ไม่มีทักษะบัฟอัตโนมัติที่ใช้งานอยู่ให้ลบ";
			next;
			goto(buffskills_menu);
		} else {
			mes "กรอกเลข ID ของทักษะที่เลือก";
			input .@buff_skillid;
			if(!getskillinfoint(1,.@buff_skillid) || !getskilllv(.@buff_skillid)){
				clear;
				mes .@menu$;
				mes "ขออภัย คุณไม่มีทักษะนั้นหรือทักษะนี้ไม่มีอยู่";
				next;
				goto(buffskills_menu);
			}
			setautoattack("3;0;"+ .@buff_skillid + ";0");
			goto(buffskills_menu);
		}
	}

	goto(config_menu);
	end;


attackskills_menu:
	clear;
	.@menu$ = "[Attack Skills]";

	mes .@menu$;
	mes "--- สถานะการใช้สกิลตั้งค่าปัจจุบัน ---";
	mes getautoattackstring(4,0);
	
	.@attackskills_menu = select("เพิ่มสกิลที่จะใช้","แก้ใขเลเวลสกิล","ปิดการใช้สกิล","[ย้อนกลับเมนูหลัก]");
	if( .@attackskills_menu < 3 ){
		if( .@attackskills_menu == 1 ){
			clear;
			mes .@menu$;
			mes "กรอกเลข ID ของทักษะที่เลือก";
			mes "--- รายการทักษะที่มีอยู่ ---";
			mes "(**ใส่แค่เลขหน้าชื่อสกิล)";
			mes getautoattackstring(4,-1);
		} else {
			if(getautoattackint(4,0) == 0){
				mes .@menu$;
				mes "ไม่มีทักษะการโจมตีอัตโนมัติให้แก้ไข";
				next;
				goto(attackskills_menu);
			}
			mes "กรอกเลข ID ของทักษะที่เลือก";
		}

		input .@attack_skillid;
		if(!getskillinfoint(2,.@attack_skillid) || !getskilllv(.@attack_skillid)){
			clear;
			mes .@menu$;
			mes "ขออภัย คุณไม่มีทักษะนั้นหรือทักษะนี้ไม่มีอยู่";
			next;
			goto(attackskills_menu);
		}	

		do{
			clear;
			mes .@menu$;
			mes "เลือกเลเวลทักษะที่จะใช้";
			mes "[" + getskillinfostring(0,.@attack_skillid) + "]" + " - Max lv : " + getskilllv(.@attack_skillid);
			input .@attack_skilllv;
		} while(.@attack_skilllv < 1 || .@attack_skilllv > getskilllv(.@attack_skillid));

		setautoattack("4;1;"+ .@attack_skillid + ";" + .@attack_skilllv);
		clear;

		goto(attackskills_menu);
	} else if(.@attackskills_menu == 3) {
		if(getautoattackint(4,0) == 0){
			mes .@menu$;
			mes "ไม่มีทักษะการโจมตีอัตโนมัติที่ใช้งานอยู่ให้ลบ";
			next;
			goto(attackskills_menu);
		} else {
			mes "กรอกเลข ID ของทักษะที่ต้องการลบออกในระบบ AI";
			input .@attack_skillid;
			if(!getskillinfoint(2,.@attack_skillid) || !getskilllv(.@attack_skillid)){
				clear;
				mes .@menu$;
				mes "ขออภัย คุณไม่มีทักษะนั้นหรือทักษะนี้ไม่มีอยู่";
				next;
				goto(attackskills_menu);
			}
			setautoattack("4;0;"+ .@attack_skillid + ";0");
			goto(attackskills_menu);
		}
	}

	goto(config_menu);
	end;

buffitems_menu:
	clear;
	.@menu$ = "[Buff Items]";

	mes .@menu$;
	mes "--- สถานะตั้งค่าปัจจุบัน ---";
	mes "--- รายการ Item Buff ---";
	mes getautoattackstring(5,656);
	mes getautoattackstring(5,645);
	mes getautoattackstring(5,657);
	mes getautoattackstring(5,12208);
	mes getautoattackstring(5,14592);
	mes getautoattackstring(5,12210);
	mes getautoattackstring(5,12883);
	mes getautoattackstring(5,14766);
	mes getautoattackstring(5,12796);
	mes getautoattackstring(5,12215);
	mes getautoattackstring(5,12216);
	mes getautoattackstring(5,12411);
	mes getautoattackstring(5,12412);

	.@buffitems_menu = select("เพิ่ม / แก้ไข Item Buff","ปิดการใช้ Item Buff","[ย้อนกลับเมนูหลัก]");
	if( .@buffitems_menu == 1 ){
		mes "กรอก Item Buff ที่ต้องการเลือกใช้";

		do {
			input .@buffitem_id;
		} while(.@buffitem_id != 656 && .@buffitem_id != 645 && .@buffitem_id != 657 && .@buffitem_id != 12208 && .@buffitem_id != 14592 && .@buffitem_id != 12210 && .@buffitem_id != 12883 && .@buffitem_id != 14766 && .@buffitem_id != 12796 && .@buffitem_id != 12215 && .@buffitem_id != 12216 && .@buffitem_id != 12411 && .@buffitem_id != 12412);

		do {
			clear;
			mes .@menu$;
			mes "ตั้งค่าเวลาในการใช้";
			mes "ตัวอย่าง : ใส่เลข 1800 จะใช้ Item Buff ทุกๆ 30 นาที (60=1 นาที)";
			mes "เลขต่ำสุดที่ใส่ 5, เลขมากสุดที่ใส่ 3600";
			input .@buffitem_delayvalue;
		} while(.@buffitem_delayvalue < 5 || .@buffitem_delayvalue > 3600);

		setautoattack("5;1;"+ .@buffitem_id + ";" + .@buffitem_delayvalue);
		clear;

		goto(buffitems_menu);
	}
	if( .@buffitems_menu == 2 ){
		if(getautoattackint(5,0) == 0){
			clear;
			mes .@menu$;
			mes "ไม่มีรายการบัฟที่ใช้งานอยู่ให้ลบ";
			next;
			goto(buffitems_menu);
		} else {
			mes "กรุณากรอกรหัสรายการบัฟเพื่อปิดการใช้งาน.";

			do {
				input .@buffitem_id;
			} while(.@buffitem_id != 656 && .@buffitem_id != 645 && .@buffitem_id != 657 && .@buffitem_id != 12208 && .@buffitem_id != 14592 && .@buffitem_id != 12210 && .@buffitem_id != 12883 && .@buffitem_id != 14766 && .@buffitem_id != 12796 && .@buffitem_id != 12215 && .@buffitem_id != 12216 && .@buffitem_id != 12411 && .@buffitem_id != 12412);

			setautoattack("5;0;"+ .@buffitem_id + ";0");
			goto(buffitems_menu);
		}
	}

	goto(config_menu);
	end;

OnDo2:
	sc_end SC_AUTOATTACK;
	message strcharinfo(0),"จบการทำงาน";
	end;
	function	__onoff	{
		if(getarg(0) == 0)
			return "^BEBEBE[OFF]^000000";
		else
			return "^000088[ON]^000000";
	}
}